apply plugin: 'com.android.application'

android {
    compileSdkVersion project.ext.mycompileSdkVersion
    project.archivesBaseName = "Routewise"

    defaultConfig {
        applicationId project.ext.myApplicationId
        minSdkVersion project.ext.myminSdkVersion
        targetSdkVersion project.ext.mytargetSdkVersion
        versionCode project.ext.myVersionCode
        versionName project.ext.myVersionName
        setProperty("archivesBaseName", project.archivesBaseName + "_v" + versionName)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("${rootDir}\\" + RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            buildConfigField "boolean", "RELEASE", "true"
            buildConfigField "boolean", "MOCK_DRIVING", "false"
            buildConfigField "boolean", "MOCK_LOCATIONS", "false"
            buildConfigField "String", "DOMAIN", "\"routewise.tnedicca.com\""
            buildConfigField "String", "REST_URL", "\"https://routewise.tnedicca.com/\""
            resValue "string", "google_maps_key", "AIzaSyCyROjJwG4w7IbR9onBirXk45wCn4eII0c"
            resValue "string", "google_static_maps_key", "AIzaSyDe8jYsuQ42AMud6pBotU-zlk68mg4hxSI"
            resValue "string", "google_places_key", "AIzaSyCjAuUzblHQgBoF0j_VO2HDq694dGAPGGQ"

            signingConfig signingConfigs.release

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        prerelease {
            initWith release
            versionNameSuffix "-prerelease"

            buildConfigField "String", "DOMAIN", "\"routewisedev1.tnedicca.com\""
            buildConfigField "String", "REST_URL", "\"https://routewisedev1.tnedicca.com/\""
            resValue "string", "google_maps_key", "AIzaSyCiSqrPkoSk4vBbuwvq2VikzJghMZBl-aY"
            resValue "string", "google_static_maps_key", "AIzaSyDqgYHsNeNwGOIr0kK2hbsmK6bWCB7Zwwk"
            resValue "string", "google_places_key", "AIzaSyDyvPkeO3rv0ebv37pLmRHyo7QqTFIDoeQ"
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            buildConfigField "boolean", "RELEASE", "false"
            buildConfigField "boolean", "MOCK_DRIVING", "false"
            buildConfigField "boolean", "MOCK_LOCATIONS", "false"
//            buildConfigField "String", "DOMAIN", "\"routewise.tnedicca.com\""
//            buildConfigField "String", "REST_URL", "\"https://routewise.tnedicca.com/\""
            buildConfigField "String", "DOMAIN", "\"routewisedev.tnedicca.com\""
            buildConfigField "String", "REST_URL", "\"https://routewisedev.tnedicca.com/\""
            resValue "string", "google_maps_key", "AIzaSyCiSqrPkoSk4vBbuwvq2VikzJghMZBl-aY"
            resValue "string", "google_static_maps_key", "AIzaSyDqgYHsNeNwGOIr0kK2hbsmK6bWCB7Zwwk"
            resValue "string", "google_places_key", "AIzaSyDyvPkeO3rv0ebv37pLmRHyo7QqTFIDoeQ"
        }

        applicationVariants.all {
            variant ->
                variant.outputs.all {
                    outputFileName = "${project.archivesBaseName}.apk"

                    if (variant.getBuildType().isMinifyEnabled()) {
                        variant.assembleProvider.get().doLast {
                            copy {
                                from variant.mappingFile
                                into "${rootDir}/mapping"
                                rename { String fileName ->
                                    "${project.archivesBaseName}_mapping.txt"
                                }
                            }
                        }
                    }
                }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

//    implementation 'com.android.volley:volley:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.libraries.places:places:2.0.0'

    implementation 'com.kyleduo.switchbutton:library:1.4.4'
//    implementation 'com.kyleduo.switchbutton:library:2.0.0'
    testImplementation 'junit:junit:4.12'
    implementation project(':library')
}